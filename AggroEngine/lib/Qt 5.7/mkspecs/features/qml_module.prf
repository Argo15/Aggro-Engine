#
#  W A R N I N G
#  -------------
#
# This file is not part of the Qt API.  It exists purely as an
# implementation detail.  It may change from version to version
# without notice, or even be removed.
#
# We mean it.
#

equals(TEMPLATE, app): TEMPLATE = aux

isEmpty(TARGETPATH): error("Must set TARGETPATH (QML import name)")

qmldir_file = $$_PRO_FILE_PWD_/qmldir
fq_qml_files = $$qmldir_file

for(qmlf, QML_FILES): fq_qml_files += $$absolute_path($$qmlf, $$_PRO_FILE_PWD_)

qml1_target: \
    instbase = $$[QT_INSTALL_IMPORTS]
else: \
    instbase = $$[QT_INSTALL_QML]

!qml1_target:static: CONFIG += builtin_resources

builtin_resources {
    URITARGET = $$replace(URI, "\\.", "_")
    # Ensure the qml files are included in the resources
    $${URITARGET}.files = $$fq_qml_files
    # qt-project.org/imports is the path used for locating imports inside the resources
    $${URITARGET}.prefix = /qt-project.org/imports/$$TARGETPATH
    RESOURCES += $${URITARGET}
}

# Install rules
qmldir.base = $$_PRO_FILE_PWD_
!builtin_resources: qmldir.files = $$fq_qml_files
else: qmldir.files = $$qmldir_file
qmldir.path = $$instbase/$$TARGETPATH
INSTALLS += qmldir

!prefix_build: COPIES += qmldir
